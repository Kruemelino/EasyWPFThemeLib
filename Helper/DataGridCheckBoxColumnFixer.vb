Namespace Helpers

    ''' <summary>
    ''' <see href="https://github.com/dotnetprojects/WpfToolkit/blob/dbe5005dd7b0734bd9947b7f41f49ed60037e224/WpfToolkit/WPF.Themes/JetPack/Helpers/DataGridCheckBoxColumnFixer.cs"/>
    ''' <see href="http://www.jonathanantoine.com/2011/09/06/wpfs-datagridcheckboxcolumn-elementstyle-uses-a-wrong-default-value/"/>
    ''' </summary>
    Public Class DataGridCheckBoxColumnFixer
        Inherits DependencyObject

        Public Shared ReadOnly FixProperty As DependencyProperty = DependencyProperty.RegisterAttached("Fix", GetType(Boolean), GetType(DataGridCheckBoxColumnFixer), New FrameworkPropertyMetadata(False, AddressOf OnFixChanged))
        Private Shared _defaultElementStyle As Style
        Private Shared _defaultEditingElementStyle As Style

        Public Shared Function GetFix(d As DependencyObject) As Boolean
            Return d.GetValue(FixProperty)
        End Function

        Public Shared Sub SetFix(d As DependencyObject, value As Boolean)
            d.SetValue(FixProperty, value)
        End Sub

        Private Shared Sub OnFixChanged(d As DependencyObject, e As DependencyPropertyChangedEventArgs)
            Dim oldFix As Boolean = e.OldValue
            Dim newFix As Boolean = d.GetValue(FixProperty)
            If d Is Nothing Then Return
            Dim dataGrid As DataGrid = TryCast(d, DataGrid)
            If oldFix Then Unsubscribe(dataGrid)
            If Not newFix Then Return
            If dataGrid Is Nothing Then Return
            If Not dataGrid.AutoGenerateColumns Then Return

            AddHandler dataGrid.AutoGeneratingColumn, AddressOf DataGridAutoGeneratingColumn
            AddHandler dataGrid.AutoGeneratedColumns, AddressOf DataGridAutoGeneratedColumns

        End Sub

        Private Shared Sub DataGridAutoGeneratedColumns(sender As Object, e As EventArgs)
            Dim dataGrid = TryCast(sender, DataGrid)
            Unsubscribe(dataGrid)
        End Sub

        Private Shared Sub Unsubscribe(dataGrid As DataGrid)
            If dataGrid IsNot Nothing Then
                RemoveHandler dataGrid.AutoGeneratingColumn, AddressOf DataGridAutoGeneratingColumn
                RemoveHandler dataGrid.AutoGeneratedColumns, AddressOf DataGridAutoGeneratedColumns
            End If
        End Sub

        Public Shared ReadOnly Property DefaultElementStyle As Style
            Get
                If _defaultElementStyle Is Nothing Then
                    Dim baseStyle As Style = TryCast(Windows.Application.Current.FindResource(GetType(CheckBox)), Style)
                    Dim style As Style

                    If baseStyle IsNot Nothing Then
                        style = New Style(GetType(CheckBox), baseStyle)
                    Else
                        style = New Style(GetType(CheckBox))
                    End If

                    style.Setters.Add(New Setter(FrameworkElement.HorizontalAlignmentProperty, HorizontalAlignment.Center))
                    style.Setters.Add(New Setter(FrameworkElement.VerticalAlignmentProperty, VerticalAlignment.Top))
                    style.Seal()
                    _defaultElementStyle = style
                End If

                Return _defaultElementStyle
            End Get
        End Property

        Public Shared ReadOnly Property DefaultEditingElementStyle As Style
            Get

                If _defaultEditingElementStyle Is Nothing Then
                    Dim baseStyle As Style = TryCast(Windows.Application.Current.FindResource(GetType(CheckBox)), Style)
                    Dim style As Style

                    If baseStyle IsNot Nothing Then
                        style = New Style(GetType(CheckBox), baseStyle)
                    Else
                        style = New Style(GetType(CheckBox))
                    End If

                    style.Setters.Add(New Setter(UIElement.IsHitTestVisibleProperty, False))
                    style.Setters.Add(New Setter(UIElement.FocusableProperty, False))
                    style.Setters.Add(New Setter(FrameworkElement.HorizontalAlignmentProperty, HorizontalAlignment.Center))
                    style.Setters.Add(New Setter(FrameworkElement.VerticalAlignmentProperty, VerticalAlignment.Top))
                    style.Seal()
                    _defaultEditingElementStyle = style
                End If

                Return _defaultEditingElementStyle
            End Get
        End Property

        Private Shared Sub DataGridAutoGeneratingColumn(sender As Object, e As DataGridAutoGeneratingColumnEventArgs)
            If Not (TypeOf e.Column Is DataGridCheckBoxColumn) Then Return
            Dim checkBoxColumn = TryCast(e.Column, DataGridCheckBoxColumn)
            checkBoxColumn.ElementStyle = DefaultElementStyle
            checkBoxColumn.EditingElementStyle = DefaultEditingElementStyle
        End Sub
    End Class
End Namespace
